start:
  if (hasRegistered) {
    say "Actually I don't need you to register again, you cn visit our website!"
    say Url("https://csml.dev")
    goto end
  } else if (email) goto confirm2
  else if (name) goto email
  
  if (!user_id) remember user_id = Fn("utils/uuid", version="v4")
  say "Hi {{_metadata.firstname}}, let me help you register to our awesome event ðŸŽ‰!"
  do Fn("google/analytics", 
  method="sendEvent", 
  params={"user_id": user_id,
  "category": "csml_event_chatbot",
  "action": "flow_process",
  "label": "start"})
  goto name

name:
  // Asking a question
  say "What's your name?"
  // Waiting for the user's input
  hold
  // We remember what the user said
  remember name = event
  // We go to the next step
  do Fn("google/analytics", 
  method="sendEvent", 
  params={"user_id": user_id,
  "category": "csml_event_chatbot",
  "action": "flow_process",
  "label": "name"})
  goto email

email:
  say "What's your email address?"
  hold
  if (!event.contains("@")) {
    say "Please enter a valid email address"
    goto email
  }
  remember email = event
  do Fn("google/analytics", 
  method="sendEvent", 
  params={"user_id": user_id,
  "category": "csml_event_chatbot",
  "action": "flow_process",
  "label": "email"})
  goto confirm2

confirm2:
  say Question("Is it ok if we contact you in the future? ðŸ˜‡ðŸ˜‡ðŸ˜‡",
    button_type="quick_reply",
    buttons=[
      Button("Yes") as btnyes,
      Button("No") as btnno
  ])
  hold
  if (event match btnno) say "Ok no problem!"
  remember optin = event
  do Fn("google/analytics", 
  method="sendEvent", 
  params={"user_id": user_id,
  "category": "csml_event_chatbot",
  "action": "flow_process",
  "label": "optin2"})
  goto save

save:
  say "Ok **{{name}}**, I have registered your email **{{email}}** to our event!"
  say "Enjoy the great content ðŸŽ‰"
  do Fn("google/analytics", 
  method="sendEvent", 
  params={"user_id": user_id,
  "category": "csml_event_chatbot",
  "action": "flow_process",
  "label": "end"})
  do Fn("zapier", hook="myhook/myhookid", payload={
      "name": name,
      "email": email,
      "optin": optin})
  remember hasRegistered = true
  goto end
